plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'com.danifoldi'
version '1.0.0'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://repo.dmulloy2.net/repository/public/' }
    maven { url 'https://repo.janmm14.de/repository/public/' }
}

dependencies {
    compileOnly ('com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT') {
        exclude module: 'guava'
        exclude module: 'jsr305'
    }

    compileOnly 'org.jetbrains:annotations:20.1.0'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.6.0'
    compileOnly 'de.janmm14:aac-api:5.0.0'
    implementation 'javax.inject:javax.inject:1'
    implementation 'com.google.dagger:dagger:2.33'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.33'

    implementation ('com.electronwill.night-config:yaml:3.6.0') {
        exclude module: 'snakeyaml'
    }
}

processResources {
    from('src/main/resources') {
        expand(version: project.version)
    }
}

shadowJar {
    minimize {
        exclude(dependency('com.electronwill.night-config:.*:.*'))
    }

    relocate 'com.electronwill.nightconfig', 'com.danifoldi.cpscheck.aac.lib.config'
    relocate 'javax.inject', 'com.danifoldi.cpscheck.aac.lib.inject'
    relocate 'dagger', 'com.danifoldi.cpscheck.aac.lib.dagger'

    getArchiveFileName().set("${project.name}-${project.version}.jar")
}